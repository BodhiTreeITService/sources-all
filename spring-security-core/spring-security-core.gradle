import java.util.concurrent.Callable

apply plugin: 'io.spring.convention.spring-module'

dependencies {
	management platform(project(":spring-security-dependencies"))
	api project(':spring-security-crypto')
	api project(':spring-aop')
	api project(':spring-beans')
	api project(':spring-context')
	api project(':spring-core')
	api project(':spring-expression')
	api 'io.micrometer:micrometer-observation'

	optional 'com.fasterxml.jackson.core:jackson-databind'
	optional 'io.projectreactor:reactor-core'
	optional 'jakarta.annotation:jakarta.annotation-api:2.0.0'
	optional 'org.aspectj:aspectjrt'
	optional project(':spring-jdbc')
	optional project(':spring-tx')
	optional 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor'

	testImplementation 'commons-collections:commons-collections'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation "org.assertj:assertj-core"
	testImplementation "org.junit.jupiter:junit-jupiter-api"
	testImplementation "org.junit.jupiter:junit-jupiter-params"
	testImplementation "org.junit.jupiter:junit-jupiter-engine"
	testImplementation "org.mockito:mockito-core"
	testImplementation "org.mockito:mockito-junit-jupiter"
	testImplementation project(":spring-test")
	testImplementation 'org.skyscreamer:jsonassert'
	testImplementation project(':spring-test')

	testRuntimeOnly 'org.hsqldb:hsqldb'
}

task springVersion(type: org.gradle.api.tasks.WriteProperties) {
	destinationFile = file("${buildDir}/versions/spring-security.versions")
	property("org.springframework:spring-core", springVersion())
}

tasks.processResources {
	into('META-INF') {
		from project.tasks.springVersion.outputs
	}
}

configure(project.tasks.withType(Test)) {
	doFirst {
		systemProperties['springSecurityVersion'] = version
		systemProperties['springVersion'] = springVersion().call()
	}
}

Callable<String> springVersion() {
	return  (Callable<String>) { project.configurations.compileClasspath.resolvedConfiguration.resolvedArtifacts
    .find { it.name == 'spring-core' }.moduleVersion.id.version }
}
